# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//arkcompiler/runtime_core/static_core/ark_config.gni")

declare_args() {
  if (is_ohos && is_standard_system) {
    ark_napi_headers_path = "//third_party/node/src"
  } else {
    ark_napi_headers_path = ""
  }
}
assert(ark_napi_headers_path != "",  "ark_napi_headers_path is not set")

if (is_ohos && is_standard_system) {
  ohos_prebuilt_etc("etsstdlib_install") {
    deps = ["$ark_root/plugins/ets:etsstdlib"]
    stdlib_dir = get_label_info("$ark_root/plugins/ets:etsstdlib", "target_gen_dir")
    source = "$stdlib_dir/etsstdlib.abc"
    part_name = ark_part_name
    subsystem_name = ark_subsystem_name
  }
}

ohos_shared_library("ets_interop_js_napi") {
  configs = [
    "$ark_root:ark_config",
  ]

  include_dirs = [
    "$ark_napi_headers_path",
  ]

  sources = [
    "ets_vm_plugin.cpp",
    "js_value.cpp",
    "js_refconvert.cpp",
    "ets_proxy/ets_class_wrapper.cpp",
    "ets_proxy/ets_field_wrapper.cpp",
    "ets_proxy/ets_method_wrapper.cpp",
    "ets_proxy/shared_reference.cpp",
    "ets_proxy/shared_reference_storage.cpp",
    "ets_proxy/ets_proxy.cpp",
    "interop_context.cpp",
    "intrinsics_api_impl.cpp",
    "js_value_call.cpp",
    "interop_common.cpp",
    "ts2ets_copy.cpp",
    "ts2ets_tstype.cpp",
    "js_job_queue.cpp",
    "pending_promise_listener.cpp",
    "js_refconvert_builtin.cpp",
    "js_proxy/js_proxy.cpp"
  ]
  if (current_cpu == "amd64" || current_cpu == "x64" || current_cpu == "x86_64") {
    sources += [
      "arch/amd64/ts2ets_jscall_bridge_amd64.S",
    ]
  } else if (current_cpu == "arm64") {
    sources += [
      "arch/arm64/ts2ets_jscall_bridge_aarch64.S",
    ]
  } else if (current_cpu == "arm") {
    sources += [
      "arch/arm32/ts2ets_jscall_bridge_arm32.S",
    ]
  }

  deps = [
    "$ark_root/runtime:libarkruntime",
    "$ark_root/libpandabase:libarktsbase",
    "$ark_root/compiler:libarktscompiler"
  ]
  if (is_ohos && is_standard_system) {
    include_dirs += [ "//base/hiviewdfx/hilog/interfaces/native/innerkits/include" ]
    defines = [ "PANDA_USE_OHOS_LOG" ]
    deps += [":etsstdlib_install"]
    external_deps = [
      "napi:ace_napi",
      "hilog:libhilog"
    ]
  }
  relative_install_dir = "module"
  part_name = ark_part_name
  subsystem_name = "$ark_subsystem_name"
}
